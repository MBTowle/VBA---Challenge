Sub stock()

' Mary Towle
' Homework 2 VBA


'****************Loop for multiple worksheets *********************************************
For Each ws In Worksheets


'**************** Individual Looping for Each Sheet ***************************************
'Set an initial variable for holding the ticker
Dim Ticker As String

'Set an initial variable for holding the ticker opening price and set to have opening value of first ticker
Dim Ticker_Open As Double
Ticker_Open = ws.Range("C2").Value

'Set an initial variable for holding the ticker closing price
Dim Ticker_Close As Double

'Set an initial variable for percentage change
Dim Ticker_Percentage_Change As Double
Ticker_Percentage_Change = 0

'Set an initial variable for total stock volume
Dim Ticker_Total_Volume As Double
Ticker_Total_Volume = 0

'Set an initial variable for holding the total change per ticker
Dim Ticker_Change As Double
Ticker_Change = 0

'Keep track of the location for each ticker in the summary table
Dim Summary_Table_Row As Integer
Summary_Table_Row = 2

'Set an initial variable for greatest percentage increase
Dim Greatest_Increase_Percentage As Double
Greatest_Increase_Percentage = 0

'Set an initial variable for greatest percentage increase
Dim Greatest_Decrease_Percentage As Double
Greatest_Decrease_Percentage = 0

'Set an initial variable for greatest percentage increase
Dim Greatest_Total_Volume As Double
Greatest_Total_Volume = 0

'Print Ticker Summary Headers
ws.Range("I1").Value = "Ticker"
ws.Range("J1").Value = "Yearly Change"
ws.Range("K1").Value = "Percent Change"
ws.Range("L1").Value = "Total Stock Volume"

'Print Greatest Headers and Labels
ws.Range("P1").Value = "Ticker"
ws.Range("Q1").Value = "Value"
ws.Range("O2").Value = "Greatest % Increase"
ws.Range("O3").Value = "Greatest % Decrease"
ws.Range("O4").Value = "Greatest Total Volume"

'Find last row of sheet
Last_Row = ws.Range("A1").CurrentRegion.Rows.Count

'Loop through all ticker amount changes
For i = 2 To Last_Row

'Check to see if we are still with the same ticker.  If not then
If ws.Cells(i + 1, 1).Value <> ws.Cells(i, 1).Value Then

    'Update the ticker close amount
    Ticker_Close = (ws.Cells(i, 6).Value)
    
    'Update the ticker change amount by subtracting the close to the original open
    Ticker_Change = Ticker_Close - Ticker_Open
    
    'Update the percentage amount of change
    'Account for if opening amount was 0 and no percentage multiplication is possible
    If Ticker_Open > 0 Then
        Ticker_Percentage_Change = Ticker_Change / Ticker_Open
    Else
        Ticker_Percentage_Change = 0
    End If
    
    'Update the total volumne amount of ticker
    Ticker_Total_Volume = Ticker_Total_Volume + ws.Cells(i, 7).Value
   
    'Print the ticker in the summary table
    ws.Range("I" & Summary_Table_Row).Value = Ticker
        
    'Print the yearly change in the summary table
    ws.Range("J" & Summary_Table_Row).Value = Ticker_Change
        If Ticker_Change < 0 Then
            ws.Range("J" & Summary_Table_Row).Interior.ColorIndex = 3
        ElseIf Ticker_Percentage_Change > 0 Then
            ws.Range("J" & Summary_Table_Row).Interior.ColorIndex = 4
        End If
    
    'Print the percentage change in the summary table
    ws.Range("K" & Summary_Table_Row).Value = Ticker_Percentage_Change
    ws.Range("K" & Summary_Table_Row).NumberFormat = "0.00%"
    
    'Print the total volume of the ticker
    ws.Range("L" & Summary_Table_Row).Value = Ticker_Total_Volume
    
    'Update Greatest percentage increase
    If Ticker_Percentage_Change > Greatest_Increase_Percentage Then
        Greatest_Increase_Percentage = Ticker_Percentage_Change
        ws.Range("P2").Value = Ticker
        ws.Range("Q2").Value = Greatest_Increase_Percentage
        ws.Range("Q2").NumberFormat = "0.00%"
    End If
    
    'Update Greatest percentage decrease
    If Ticker_Percentage_Change < Greatest_Decrease_Percentage Then
        Greatest_Decrease_Percentage = Ticker_Percentage_Change
        ws.Range("P3").Value = Ticker
        ws.Range("Q3").Value = Greatest_Decrease_Percentage
        ws.Range("Q3").NumberFormat = "0.00%"
    End If
    
    'Update Greatest total volume
    If Ticker_Total_Volume > Greatest_Total_Volume Then
        Greatest_Total_Volume = Ticker_Total_Volume
        ws.Range("P4").Value = Ticker
        ws.Range("Q4").Value = Greatest_Total_Volume
    End If
    
    'Add one to the summary table row
    Summary_Table_Row = Summary_Table_Row + 1
    
    'Reset the ticker change total
    Ticker_Change = 0
    
    'Reset the ticker volume total
    Ticker_Total_Volume = 0
    
    'Reset opening price for next ticker
    Ticker_Open = (ws.Cells(i + 1, 3).Value)
        
    
'If the cell immediately following a row is the same brand then
Else
    'Set Ticker
    Ticker = ws.Cells(i, 1).Value
    
    'Add to the ticker total volume
    Ticker_Total_Volume = Ticker_Total_Volume + ws.Cells(i, 7).Value


End If

Next i


Next ws
    
End Sub
